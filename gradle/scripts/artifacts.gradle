def includePath = "org/squiddev/cctweaks/api/**";

task sourcesJar(type: Jar, dependsOn:classes) {
	from sourceSets.main.allSource
	include includePath

	classifier = "sources"
}

// Build the Javadocs for the API
task javadocAPI(type: Javadoc) {
	source = sourceSets.main.java
	include includePath

	classpath = configurations.compile
}

// Package Javadocs into a Jar
task javadocJar(type: Jar, dependsOn:javadocAPI) {
	from javadocAPI.destinationDir

	classifier = "javadoc"
}

// Build a non-obsfucated jar
task devJar(type: Jar) {
	manifest {
		attributes(
			'FMLCorePlugin': 'org.squiddev.cctweaks.core.asm.TweaksLoadingPlugin',
			'FMLCorePluginContainsFMLMod': 'true'
		)
	}

	from sourceSets.main.output

	// Package all the org.squiddev dependencies into one file
	from configurations.fatPackage
		.findAll { it.getAbsolutePath().contains("org.squiddev") }
		.collect { it.isDirectory() ? it : zipTree(it) }

	// Package the patch files into a patch/ folder
	filesMatching("org/luaj/**", {
		it.path = "patch/" + it.path
	})

	classifier = 'dev'
}

// Build an API jar
task apiJar(type: Jar) {
	from sourceSets.main.output
	include includePath

	classifier = 'api'
}

// All the artifacts to build
artifacts {
	archives jar

	archives devJar
	archives apiJar
	archives sourcesJar
	archives javadocJar
}
